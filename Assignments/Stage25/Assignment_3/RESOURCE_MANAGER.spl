alias functionNum R1;
alias PID R2;

if(functionNum == ACQUIRE_BUFFER) then
    alias buffNo R3;
    
    while([BUFFER_TABLE + buffNo*4 + 2] != -1) do
        [PROCESS_TABLE + PID*16 + 4] = WAIT_BUFFER;
        [PROCESS_TABLE + PID*16 + 5] = buffNo;
        multipush(R1, R2, R3);
        call SCHEDULER;
        multipop(R1, R2, R3);
    endwhile;

    [BUFFER_TABLE + buffNo*4 + 2] = PID;

    return;
endif;

if(functionNum == RELEASE_BUFFER) then
    alias buffNo R3;
    alias counter R4;

    // Check Locking PID == PID
    if([BUFFER_TABLE + buffNo*4 + 2] != PID) then
        R0 = -1;
        return;
    endif;

    // Reset the lock
    [BUFFER_TABLE + buffNo*4 + 2] = -1;

    // Loop through Process table
    counter = 0;
    while (counter < MAX_PROC_NUM) do
        if(([PROCESS_TABLE + counter*16 + 4] == WAIT_BUFFER) && ([PROCESS_TABLE + counter*16 + 5] == buffNo)) then
            [PROCESS_TABLE + counter*16 + 4] = READY;
        endif;
        counter = counter + 1;
    endwhile;

    R0 = 0;
    return; 
endif;

if (functionNum == ACQUIRE_DISK) then
    while ([DISK_STATUS_TABLE] == 1) do
        [PROCESS_TABLE + PID*16 + 4] = WAIT_DISK;
        multipush(R1, R2);
        call SCHEDULER;
        multipop(R1, R2);
    endwhile;
    [DISK_STATUS_TABLE] = 1;
    [DISK_STATUS_TABLE + 4] = PID;
    return;
endif;

if (functionNum == ACQUIRE_INODE) then
    alias inodeIdx R3;

    // BUSY WAIT
    while([FILE_STATUS_TABLE + inodeIdx*4] != -1) do
        [PROCESS_TABLE + PID*16 + 4] = WAIT_FILE;
        [PROCESS_TABLE + PID*16 + 5] = inodeIdx;
        multipush(R1, R2, R3);
        call SCHEDULER;
        multipop(R1, R2, R3);
    endwhile;

    // If file is already deleted
    if([INODE_TABLE + inodeIdx*16 + 1] == -1) then
        R0 = -1;
        return;
    endif;

    [FILE_STATUS_TABLE + inodeIdx*4] = PID;
    R0 = 0;
    return; 
endif;

if (functionNum == RELEASE_INODE) then
    alias inodeIdx R3;
    alias counter R4;

    // Check Locking PID == PID
    if([FILE_STATUS_TABLE + inodeIdx*4] != PID) then
        R0 = -1;
        return;
    endif;
    // Reset the lock
    [FILE_STATUS_TABLE + inodeIdx*4] = -1;

    // Loop through Process table
    counter = 0;
    while (counter < MAX_PROC_NUM) do
        if(([PROCESS_TABLE + counter*16 + 4] == WAIT_FILE) && ([PROCESS_TABLE + counter*16 + 5] == inodeIdx)) then
            [PROCESS_TABLE + counter*16 + 4] = READY;
        endif;
        counter = counter + 1;
    endwhile;

    R0 = 0;
    return; 
endif;

if (functionNum == ACQUIRE_TERMINAL) then
    // Busy Wait
    while ([TERMINAL_STATUS_TABLE] == 1) do
        [PROCESS_TABLE + PID*16 + 4] = WAIT_TERMINAL;
        multipush(R1, R2);
        call SCHEDULER;
        multipop(R1, R2);
    endwhile;
    // Lock Terminal
    [TERMINAL_STATUS_TABLE] = 1;
    [TERMINAL_STATUS_TABLE + 1] = PID;
    return;
endif;

if (functionNum == RELEASE_TERMINAL) then
    if (PID != [TERMINAL_STATUS_TABLE + 1]) then
        R0 = -1;
        return;
    endif;
    [TERMINAL_STATUS_TABLE] = 0;
    alias counter R3;
    counter = 0;
    while (counter < MAX_PROC_NUM) do
        if ([PROCESS_TABLE + counter*16 + 4] == WAIT_TERMINAL) then
            [PROCESS_TABLE + counter*16 + 4] = READY;
        endif;
        counter = counter + 1;
    endwhile;
    R0 = 0;
    return;
endif;

if (functionNum == ACQUIRE_SEMAPHORE) then
    alias counter R10;
    counter = 0;
    while(counter < MAX_SEM_COUNT) do
        if([SEMAPHORE_TABLE + counter*4 + 1] == 0) then
            [SEMAPHORE_TABLE + counter*4 + 0] = -1;
            [SEMAPHORE_TABLE + counter*4 + 1] = 1;
            R0 = counter;
            return;
        endif;
        counter = counter + 1;
    endwhile;
    R0 = -1;
    return;
endif;

if (functionNum == RELEASE_SEMAPHORE) then
    alias SEMID R3;         
    alias counter R10;
    if([SEMAPHORE_TABLE + SEMID*4] == PID) then
        // Reset LOCKING PID
        [SEMAPHORE_TABLE + SEMID*4] = -1;
        // Make waiting processes READY
        counter = 0;
        while(counter < MAX_PROC_NUM) do
            if(([PROCESS_TABLE + counter*16 + 4] == WAIT_SEMAPHORE) && ([PROCESS_TABLE + counter*16 + 5] == SEMID)) then
                [PROCESS_TABLE + counter*16 + 4] = READY;
            endif;
            counter = counter + 1;
        endwhile;
    endif;
    // Decrement PROCESS_COUNT
    [SEMAPHORE_TABLE + SEMID*4 + 1] = [SEMAPHORE_TABLE + SEMID*4 + 1] - 1;
    return;
endif;
